// Base units
second = { !("s" ~ ident) ~ "s" }
meter = { !("m" ~ ident) ~ "m" }
gram = { !("g" ~ ident) ~ "g" }
ampere = { !("A" ~ ident) ~ "A" }
kelvin = { !("K" ~ ident) ~ "K" }
mole = { !("mol" ~ ident) ~ "mol" }
candela = { !("cd" ~ ident) ~ "cd" }

// Derived units
radian = { !("rad" ~ ident) ~ "rad" }
steradian = { !("sr" ~ ident) ~ "sr" }
hertz = { !("Hz" ~ ident) ~ "Hz" }
newton = { !("N" ~ ident) ~ "N" }
pascal = { !("Pa" ~ ident) ~ "Pa" }
joule = { !("J" ~ ident) ~ "J" }
watt = { !("W" ~ ident) ~ "W" }
coulomb = { !("C" ~ ident) ~ "C" }
volt = { !("V" ~ ident) ~ "V" }
farad = { !("F" ~ ident) ~ "F" }
ohm = { !("Ω" ~ ident) ~ "Ω" }
siemens = { !("S" ~ ident) ~ "S" }
weber = { !("Wb" ~ ident) ~ "Wb" }
tesla = { !("T" ~ ident) ~ "T" }
henry = { !("H" ~ ident) ~ "H" }
celsius = { "°C" } // can only be used standalone though?
lumen = { !("lm" ~ ident) ~ "lm" }
lux = { !("lx" ~ ident) ~ "lx" }
becquerel = { !("Bq" ~ ident) ~ "Bq" }
gray = { !("Gy" ~ ident) ~ "Gy" }
sievert = { !("Sv" ~ ident) ~ "Sv" }
katal = { !("kat" ~ ident) ~ "kat" }

// Units and numbers
raw_unit = {
    second | meter | gram | ampere | kelvin | mole | candela |
    radian | steradian | hertz | newton | pascal | joule | watt | coulomb | volt | farad | ohm |
    siemens | weber | tesla | henry | celsius | lumen | lux | becquerel | gray | sievert | katal | ident
}
unit = { prefix? ~ raw_unit ~ (("^" ~ integer_number) | ss_number)? }
term = { unit | number }
unit_expr = { paren | (term ~ silent_space? ~ (verb ~ silent_space? ~ unit_expr)*) }
si_expr = { SOI ~ silent_space? ~ (&(number ~ silent_space? ~ unit) ~ number)? ~ silent_space? ~ unit_expr ~ silent_space? ~ EOI }
paren = { "(" ~ silent_space ~ unit_expr ~ silent_space ~ ")" }
// binary_op = { unit_expr ~ verb ~ unit_expr }
verb = { mul | div }

si_number = { SOI ~ silent_space? ~ number ~ silent_space? ~ unit? ~ silent_space? ~ EOI }

// Prefixes
prefix = {
    deca | deci | hecto | centi | kilo | milli | mega | micro | giga | nano | tera | pico | peta | femto |
    exa | atto | zetta | zepto | yotta | yocto | ronna | ronto | quetta | quecto
}
quetta = { &("Q" ~ ident) ~ "Q" } // 30
ronna = { &("R" ~ ident) ~ "R" } // 27
yotta = { &("Y" ~ ident) ~ "Y" } // 24
zetta = { &("Z" ~ ident) ~ "Z" } // 21
exa = { &("E" ~ ident) ~ "E" } // 18
peta = { &("P" ~ ident) ~ "P" } // 15
tera = { &("T" ~ ident) ~ "T" } // 12
tera_unconditional = { "T" } // 12
giga = { &("G" ~ ident) ~ "G" } // 9
giga_unconditional = { "G" } // 9
mega = { &("M" ~ ident) ~ "M" } // 6
mega_unconditional = { "M" } // 6
kilo = { &(("k" | "K") ~ ident) ~ ("k" | "K") } // 3
kilo_unconditional = { "k" | "K" } // 3
hecto = { &("h" ~ ident) ~ "h" } // 2
deca = { &("da" ~ ident) ~ "da" } // 1
deci = { &("d" ~ ident) ~ "d" } // -1
centi = { &("c" ~ ident) ~ "c" } // -2
milli = { (&("m" ~ ident) ~ "m") } // -3
milli_unconditional = { "m" }
micro = { &(("u" | "μ" | "µ") ~ ident) ~ "u" | "μ" | "µ" } // -6, U+03BC or U+00B5
micro_unconditional = { "u" | "μ" | "µ" } // -6
nano = { &("n" ~ ident) ~ "n" } // -9
nano_unconditional = { "n" } // -9
pico = { &("p" ~ ident) ~ "p" } // -12
femto = { &("f" ~ ident) ~ "f" } // -15
atto = { &("a" ~ ident) ~ "a" } // -18
zepto = { &("z" ~ ident) ~ "z" } // -21
yocto = { &("y" ~ ident) ~ "y" } // -24
ronto = { &("r" ~ ident) ~ "r" } // -27
quecto = { &("q" ~ ident) ~ "q" } // -30

// Number
number = { integer_number ~ (exponent | ("." ~ raw_number ~ exponent?) )? }
integer_number = { minus? ~ raw_number }
raw_number = { ASCII_DIGIT+ }
minus = { "-" | "\u{2212}" }
exponent = { "e" ~ integer_number }

// Operators
mul = { "*" | "\u{22C5}" }
div = { "/" }

// Special cases
ohm_or_r = { "Ω" | "R" | "r" | "Ohms" | "Ohm" }
ohm_unit = { (r_prefix? ~ ohm_or_r) | r_prefix | ohm_or_r }
r_prefix = {
    kilo_unconditional | milli_unconditional | mega_unconditional |
    micro_unconditional | giga_unconditional | nano_unconditional | tera_unconditional
}
r_number = { integer_number ~ ( exponent | ("." ~ raw_number ~ exponent?) | (r_prefix ~ raw_number?) )? }
resistance = { SOI ~ silent_space? ~ r_number ~ silent_space? ~ ohm_unit? ~ silent_space? ~ EOI }

// Superscript numbers
ss_any = { ss_zero | ss_one | ss_two | ss_three | ss_four | ss_five | ss_six | ss_seven | ss_eight | ss_nine }
ss_number = { ss_minus? ~ ss_any+ }
ss_zero = { "\u{2070}" }
ss_one = { "\u{00B9}" }
ss_two = { "\u{00B2}" }
ss_three = { "\u{00B3}" }
ss_four = { "\u{2074}" }
ss_five = { "\u{2075}" }
ss_six = { "\u{2076}" }
ss_seven = { "\u{2077}" }
ss_eight = { "\u{2078}" }
ss_nine = { "\u{2079}" }
ss_minus = { "\u{207B}" }

// Misc
ident = { ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
dot = _{ "." }

// WHITESPACE manual whitespace management
space = { " "+ } // warning if more than one space is used
silent_space = _{ " "* }
